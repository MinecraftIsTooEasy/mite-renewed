plugins {
    id 'fml-loom' version '0.1.local'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "8.1.0"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenLocal()
    maven {
        name = "Shadow"
        url = "https://maven.bawnorton.com"
    }
    maven {
        name = "Spongepowered"
        url = "https://repo.spongepowered.org/repository/maven-public/"
    }
    maven {
        name = "MixinSquared"
        url = "https://maven.bawnorton.com/releases"
    }
}

configurations {
    implementation.extendsFrom shadow
}

loom {
    mergedMinecraftJar()
    setFML(new File("libs\\fml-3.4.2.jar"))

    mods {
        "MiTE-Renewed" {
            sourceSet sourceSets.main
        }
    }

}

runClient{
    dependsOn(jar)
    args("--username", "Dev", "--session", "a.a.a.a", "--width", 800, "--height", 600)
}

runServer{
    dependsOn(jar)
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.fmlMCPMappings()

    shadow(annotationProcessor "com.github.bawnorton.mixinsquared:mixinsquared-fabric:0.2.0")
    implementation fileTree(dir: "libs", include: "**.jar")

    implementation("it.unimi.dsi:fastutil:8.5.12")
    implementation("com.google.code.gson:gson:2.11.0")
    implementation files(loom.getFML().toPath())
}

shadowJar {
    configurations = [project.configurations.shadow]
    exclude("fabric.*")
    relocate("com.bawnorton.mixinsquared", "com.github.jeffyjamzhd.renewed.mixinsquared")
    mergeServiceFiles()
    archiveFileName = "${archives_base_name} [${version}].jar"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fml.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    enabled = false
    dependsOn(shadowJar)
}

sourcesJar {
    archiveFileName = "${archives_base_name} [${version}-sources].jar"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
    }
}